import coordinacion.modulos.*
import coordinacion.*

package coordinacion.objetosactuacion {

	["m76kjua"]
	definition ObjetoActuacion is container {
		label = "Objeto de actuación";
		is-georeferenced;
		contain {
			node = ref Ficha;
			node = ref historial.Coleccion;
			node = ref componentes.Coleccion;
		}

		["m_hnaaq"]
		view {
			label = "Descripción";
			show {
				component = ref Ficha.Vista;
			}
		}

		["mf9ahxw"]
		view {
			label = "Características";
			show {
				component = ref Ficha.VistaCaracteristicas;
			}
		}

		["mastvyq"]
		view {
			label = "Componentes";
			show {
				component = ref componentes.Coleccion.Vista;
			}
		}

		["mobsrhq"]
		view {
			label = "Historias";
			show {
				component = ref historial.Coleccion.Vista;
			}
		}

		["mmlybma"]
		operation NotificarIncidencia {
			label = "Generar incidencia";
			execute() {
				var incidencia = coordinacion :: incidencias :: Incidencia :: createNew(coordinacion :: incidencias :: Coleccion :: instance);
				incidencia.setObjetoActuacion(this.toLink);
				incidencia.save;
				ClientService :: redirectUserTo(incidencia.toMonetLink(true));
			}
		}

		["mqmx4vw"]
		definition Ficha is form {
			is-component;
			label = "Objeto";

			["mqxyxnw"]
			field-text Codigo {
				label = "Codigo";
			}

			["mxvmpsg"]
			field-text Nombre {
				label = "Nombre";
			}

			["me70a3q"]
			field-picture Imagen {
				label = "Foto";
				size {
					width = 640;
					height = 480;
				}
			}

			["mnto2ja"]
			field-composite Localizacion {
				label = "Localización";

				["mme0tka"]
				field-select Lugar {
					label = "Lugar";
					source = ref coordinacion.TesauroLugar;
					
				}

				["mxy0wbg"]
				field-select Zona {
					label = "Zona";
					source = ref coordinacion.TesauroZona;
				}

				["mw5fp6a"]
				field-text Direccion {
					label = "Direccion";
				}

				["m8tvnyq"]
				field-text Telefono {
					label = "Teléfono de contacto";
				}
			}

			["mm44kka"]
			field-composite Caracteristicas {
				label = "Características técnicas";
				is-multiple;

				["m_2z39a"]
				field-select Atributo {
					label = "Atributo";
					source = ref TesauroAtributo;
				}

				["mw5hvcg"]
				field-text Valor {
					label = "Valor";
				}

				["mqoqdjq"]
				field-select Unidad {
					label = "Unidad";
					source = ref TesauroUnidades;
				}
				view {
					mode = EXPANDED;
					show {
						field = ref Atributo;
						field = ref Valor;
						field = ref Unidad;
					}
				}
			}

			["mu2_udw"]
			field-boolean Inactivo {
				label = "Inactivo";
			}
			mapping {
				index = ref Coleccion.Index;
				calculate() { var referencia = this.^reference;
					var nodo = this.^node;
					var seccionLocalizacion = nodo.localizacionField as ficha.Localizacion;
					if (seccionLocalizacion.lugar != null)
						referencia.lugar = seccionLocalizacion.lugar.^label;
					if (seccionLocalizacion.zona != null)
						referencia.zona = seccionLocalizacion.zona.^label;
					referencia.direccion = seccionLocalizacion.direccion;
					referencia.telefono = seccionLocalizacion.telefono;
					if (nodo.inactivo){					
						referencia.inactivoLabel = "Inactivo";
					}else{
						referencia.inactivoLabel = null;
					}
				}
			}
			mapping {
				index = ref coordinacion.CatalogoObjetosActuacion.Index;
				calculate() { var referencia = this.^reference;
					var nodo = this.^node;
					var ubicacion = nodo.localizacionField as Localizacion;
					if (ubicacion.lugar != null)
						referencia.lugar = ubicacion.lugar.^label;
					if (ubicacion.zona != null)
						referencia.zona = ubicacion.zona.^label;
					referencia.labelPadre = nodo.nombre;
				}
			}
			properties {
				calculate() { this.getProperties().setLabel(this.getNode().getCodigo() + " " + this.getNode().getNombre());
				}
			}

			["mevxxmg"]
			view Vista {
				
				show {
					field = ref Imagen;
					field = ref Codigo;
					field = ref Nombre;
					field = ref Localizacion;
					field = ref Inactivo;
				}
			}

			["mu7yjlq"]
			view VistaCaracteristicas {
				show {
					field = ref Caracteristicas;
				}
			}

		}

	}
}
